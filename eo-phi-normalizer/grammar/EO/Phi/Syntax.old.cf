-- ==========================================================
-- BNFC grammar for φ-programs (translated from EO)
-- ==========================================================
--
-- This grammar is a (manual) translation of Phi.g4 grammar from objectionary/eo:
-- https://github.com/objectionary/eo/blob/fa24c7cc47e1ac473fb930f3a08ccc4d88d9d1e3/eo-parser/src/main/antlr4/org/eolang/parser/Phi.g4

token Bytes ({"--"} | ["0123456789ABCDEF"] ["0123456789ABCDEF"] ({"-"} ["0123456789ABCDEF"] ["0123456789ABCDEF"])* {"--"}) ;
token Function upper (char - [" \r\n\t,.|':;!-?][}{)(⟧⟦"])* ;
token LabelId  lower (char - [" \r\n\t,.|':;!-?][}{)(⟧⟦"])* ;
token AlphaIndex ({"α0"} | {"α"} (digit - ["0"]) (digit)* ) ;

Program. Program ::= "{" [Binding] "}" ;

Formation.    Object ::= "{" [Binding] "}" ;
Application.  Object ::= AbstractObject "{" [Binding] "}" [Bindings] ;
Dispatch.     Object ::= Dispatch ;
Termination.  Object ::= "⊥";

AbstractFormation.    AbstractObject ::= "{" [Binding] "}" ;
AbstractDispatch.     AbstractObject ::= Dispatch ;
AbstractTermination.  AbstractObject ::= "⊥" ;

DispatchedFormation.    DispatchedObject ::= "{" [Binding] "}" ;
DispatchedTermination.  DispatchedObject ::= "⊥" ;

AlphaBinding.   Binding ::= Attribute "↦" Object ;
EmptyBinding.   Binding ::= Attribute "↦" "∅" ;
DeltaBinding.   Binding ::= "Δ" "⤍" Bytes ;
LambdaBinding.  Binding ::= "λ" "⤍" Function ;
separator Binding "," ;

Bindings. Bindings ::= "{" [Binding] "}" ;
separator Bindings "" ;

ObjectDispatch. Dispatch ::= DispatchedObject [Bindings] "." [Attribute] [Disp] ;
GlobalDispatch. Dispatch ::= "Φ" "." [Attribute] [Disp] ;
ThisDispatch.   Dispatch ::= "ξ" "." [Attribute] [Disp] ;

Phi.    Attribute ::= "φ" ;   -- decoratee object
Rho.    Attribute ::= "ρ" ;   -- parent object
Sigma.  Attribute ::= "σ" ;   -- ???
VTX.    Attribute ::= "ν" ;   -- ???
Label.  Attribute ::= LabelId ;
Alpha.  Attribute ::= AlphaIndex ;
separator nonempty Attribute "." ;

Disp. Disp ::= "{" [Binding] "}" Bindings "." [Attribute] ;
separator Disp "" ;
